use "../stdlib/LibC.shtk" as libc
from "../stdlib/Result.shtk" use Result, ok, fail
from "../stdlib/String.shtk" use String, string, stringFromPreAllocated
from "../stdlib/RawPointerUtils.shtk" use nullptrArray
from "../stdlib/ErrorTypes.shtk" use StdlibError, EOF, OSError

enum SeekType {
    SEEK_SET,
    SEEK_CUR,
    SEEK_END
}

struct File
{
    var libcFile: libc::FILE*
}

fn open(path: String, mode: String) -> Result[File, StdlibError]
{
    var libcFile: libc::FILE* <- libc::fopen(path.toCharArray(), mode.toCharArray())

    if not addr(libcFile)
       return fail[File, StdlibError](OSError)

    var self: File
    self.libcFile <- libcFile

    return ok[File, StdlibError](self)
}

fn readLine(self: File) -> Result[String, StdlibError]
{
    var buffer: char[] <- nullptrArray[char]()
    var bufferLength: ulong
    
    const length: long = libc::getline(buffer, bufferLength, self.libcFile)

    if length < 0 {
        if libc::feof(self.libcFile)
            return fail[String, StdlibError](EOF)

        return fail[String, StdlibError](OSError)
    }
        
    const line := stringFromPreAllocated(buffer, bufferLength, ulong`length)
    return ok[String, StdlibError](line)
}

fn read(self: File) -> Result[String, StdlibError]
{
    var contents := "".string()

    loop {
        const [line, error] := self.readLine()
        
        if error {
            if error == OSError
                return fail[String, StdlibError](OSError)
            else 
                break
        }

        contents = contents + line
    }

    return ok[String, StdlibError](contents)
}

fn seek(self: File, offset: long, whence: SeekType) -> bool
{
    return libc::fseek(self.libcFile, offset, int`whence) < 0
}

fn tell(self: File) -> Result[long, StdlibError]
{
    const pos := libc::ftell(self.libcFile)

    if pos < 0
        return fail[long, StdlibError](OSError)

    return ok[long, StdlibError](pos)
}

fn write(self: File, content: String) -> bool
{
    return libc::fputs(content.toCharArray(), self.libcFile) < 0
}

fn close(self: File) -> bool
{
    return libc::fclose(self.libcFile) < 0
}

