generic T

from "../stdlib/ReferenceCounter.shtk" use ReferenceCounter, createCounter
from "../stdlib/Slice.shtk" use Slice, allocateHeap

struct SharedPointer
{
    var _count: ReferenceCounter
    var _slice: Slice[T]
}

fn allocateHeap(capacity: ulong) -> SharedPointer
{
    var self: SharedPointer

    self._count = createCounter()
    self._slice = allocateHeap[T](capacity)

    return self
}

fn deconstructor(mut self: SharedPointer)
{
    if not self._count.isZero() return void
    self._slice.free()
}

fn subscriptItem(self: SharedPointer, index: ulong) -> T
{
    return self._slice[index]
}

fn setItem(mut self: SharedPointer, index: ulong, item: T)
{
    self._slice.setItem(index, item)
}

fn capacity(self: SharedPointer) -> ulong
{
    return self._slice.capacity()
}

fn slice(self: SharedPointer) -> Slice[T]
{
    return self._slice
}

fn copy(self: SharedPointer, items: T[], length: ulong)
{
    self._slice.copy(items, length)
}
