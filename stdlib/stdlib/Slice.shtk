generic T

use "../stdlib/LibC.shtk" as libc
from "../stdlib/Panic.shtk" use panic
from "../stdlib/RawPointerUtils.shtk" use malloc, realloc, free, addOffset, memcpy, memcpyOffset, nullptrArray

def OUT_OF_BOUNDS_ERROR "Index out of bounds error"
def NULL_POINTER_ERROR "Null pointer dereference error"
def OUT_OF_CAPACITY "Slice out of capacity error"

struct Slice
{
    var _ptr: T[]
    var _capacity: ulong
    var _heapAllocated: bool
}

fn slice(ptr: T[], capacity: ulong) -> Slice
{
    var self: Slice
    self._ptr <- ptr
    self._capacity = capacity
    self._heapAllocated = false
    return self
}

fn allocateHeap(capacity: ulong) -> Slice
{
    var self: Slice
    self._ptr <- malloc[T](capacity)
    self._capacity = capacity
    self._heapAllocated = true
    return self
}

fn expand(mut self: Slice, capacity: ulong)
{
    self._ptr <- realloc[T](self._ptr, capacity)
    self._capacity = capacity
}

fn free(mut self: Slice)
{
    if not addr(self._ptr) 
        return void

    self._deconstructor()
    free[T](self._ptr)
    self._ptr <- nullptrArray[T]()
}

fn _validateIndex(self: Slice, index: ulong) 
{
    if not addr(self._ptr)
        panic(NULL_POINTER_ERROR, 1)
    
    if index >= self._capacity
        panic(OUT_OF_BOUNDS_ERROR, 1)
}

fn _validateLength(self: Slice, length: ulong)
{
    if not addr(self._ptr)
        panic(NULL_POINTER_ERROR, 1)
    
    if length > self._capacity
        panic(OUT_OF_CAPACITY, 1)
}

fn subscriptItem(self: Slice, index: ulong) -> T
{
    self._validateIndex(index)

    return self._ptr[index]
}

fn setItem(mut self: Slice, index: ulong, item: T)
{
    self._validateIndex(index)

    self._ptr[index] = item
}

fn capacity(self: Slice) -> ulong
{
    return self._capacity
}

fn copy(mut self: Slice, items: T[], length: ulong)
{
    self._validateLength(length)
    memcpy[T](self._ptr, items, length)
}

fn copyOffset(mut self: Slice, items: T[], length: ulong, offset: ulong)
{
    self._validateLength(length+offset)
    memcpyOffset[T](self._ptr, items, length, offset)
}

fn pointer(self: Slice) -> T[]
{
    return self._ptr
}

fn _deconstructor(self: Slice)
{
    for var i: ulong = 0; i < self._capacity; i += 1 
        self._ptr[i].deconstructor()
}

fn deconstructor(self: Slice)
{
    if not addr(self._ptr) 
        return void

    if self._heapAllocated
        return void

    self._deconstructor()
}