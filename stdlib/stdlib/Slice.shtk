generic T

use "../stdlib/LibC.shtk" as libc
from "../stdlib/Panic.shtk" use panic
from "../stdlib/RawPointerUtils.shtk" use malloc, addOffset, memcpy, memcpyOffset

def OUT_OF_BOUNDS_ERROR "Index out of bounds error"
def NULL_POINTER_ERROR "Null pointer dereference error"
def OUT_OF_CAPACITY "Slice out of capacity error"

struct Slice
{
    var _ptr: T[]
    var _capacity: ulong
}

fn slice(ptr: T[], capacity: ulong) -> Slice
{
    var self: Slice
    self._ptr <- ptr
    self._capacity = capacity
    return self
}

fn allocateHeap(capacity: ulong) -> Slice
{
    return slice(malloc[T](capacity), capacity)
}

fn expand(mut self: Slice, capacity: ulong)
{
    if capacity < self._capacity
        panic(OUT_OF_CAPACITY, 1)

    const newPtr: T[] <- malloc[T](capacity)

    libc::memcpy(ubyte[]`newPtr, ubyte[]`self._ptr, self._capacity*sizeof(T))

    libc::free(ubyte*`self._ptr)

    self._ptr <- newPtr
    self._capacity = capacity
}

fn free(mut self: Slice)
{
    libc::free(ubyte*`self._ptr)
    self._ptr <- T[]`0
}

fn _validateIndex(self: Slice, index: ulong) 
{
    if addr(self._ptr) == 0
        panic(NULL_POINTER_ERROR, 1)
    
    if index >= self._capacity
        panic(OUT_OF_BOUNDS_ERROR, 1)
}

fn _validateLength(self: Slice, length: ulong)
{
    if addr(self._ptr) == 0
        panic(NULL_POINTER_ERROR, 1)
    
    if length > self._capacity
        panic(OUT_OF_CAPACITY, 1)
}

fn subscriptItem(self: Slice, index: ulong) -> T
{
    self._validateIndex(index)

    return self._ptr[index]
}

fn setItem(mut self: Slice, index: ulong, item: T)
{
    self._validateIndex(index)

    self._ptr[index] = item
}

fn capacity(self: Slice) -> ulong
{
    return self._capacity
}

fn copy(self: Slice, items: T[], length: ulong)
{
    self._validateLength(length)
    memcpy[T](self._ptr, items, length)
}

fn copyOffset(self: Slice, items: T[], length: ulong, offset: ulong)
{
    self._validateLength(length+offset)
    memcpyOffset[T](self._ptr, items, length, offset)
}

fn pointer(self: Slice) -> T[]
{
    return self._ptr
}

fn deconstructor(self: Slice)
{
    # TODO should not be called for heap allocated slice

    if not addr(self._ptr) 
        return void

    for var i: ulong = 0; i < self._capacity; i += 1 
        self._ptr[i].deconstructor()
}